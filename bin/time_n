#! /usr/bin/perl

=head1 SYNOPSIS

 time_n -n 5 perl -I lib -e 'do stuff'

=head1 DESCRIPTION

Run it (default is five times) and report child process user & system
CPU times.

This is an incomplete hack, just enough for now.

It seems to have worse precision than the Bash built-in C<time> .  I
have C<CONFIG_HZ=250> .

=cut

use Time::HiRes qw( gettimeofday tv_interval );

sub main {

    my $N = 5;
    if (@ARGV>1 && $ARGV[0] eq '-n') {
        (undef, $N) = splice @ARGV, 0, 2;
    }

    my @cmd = @ARGV;
    my %T;
    for my $n (1 .. $N) {
        # ($user,$system,$cuser,$csystem) = times;
        my @t0 = times();
        my $t0 = [gettimeofday];
        system(@cmd);
        my $t = tv_interval($t0, [gettimeofday]);
        my @t1 = times();
        warn sprintf("[w] Command returned 0x%02X\n", $?) if $?;

        # TODO: keep the squares & work out some variance
        my %t = (usr => $t1[2] - $t0[2],
                 sys => $t1[3] - $t0[3],
                 wall => $t);
        $T{n}++;
        while (my ($k, $v) = each %t) {
            $T{$k} += $v;
        }

        printf("  Run %d\t%s\n", $T{n}, join ' ', %t);
    }

    foreach my $type (qw( wall usr sys )) {
        printf("Time %s: %.3fsec/%d = %.4fsec\n", $type,
               $T{$type}, $T{n},
               $T{$type} / $T{n});
    }
}

main();
